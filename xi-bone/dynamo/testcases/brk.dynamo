/*
 Booth, Rinzel, Kiehn - 1997 - Compartmental Model of Vertebrate
 Motoneurons for Ca2+ Dependent Spiking and Plateau Potentials Under
 Physiological Treatment

 $Id: brk.dynamo,v 1.1 2006/02/23 01:35:36 ivan_raikov Exp $
 $Source: /cvsroot/rtxi/rtxi/dynamo/testcases/brk.dynamo,v $
*/

SYSTEM Booth_Rinzel_Kiehn;

TIME t;

// The time range (ms) during which Iapp will be applied to the system  
PARAMETER  t_on = 200    "Beginning time for Iapp";
PARAMETER  t_off = 800   "Ending time for Iapp";

// Conductances (mS/cm^2)
PARAMETER GNa = 120;
PARAMETER GK_dr = 100;
PARAMETER GCa_NS = 14;
PARAMETER GCa_ND = .03;
PARAMETER GK_CaS = 5;
PARAMETER GK_CaD = 1.1;
PARAMETER GCa_L = 0.33;
PARAMETER gleak = 0.51;

// Static Parameters
PARAMETER C = 1; // uF/cm^2
PARAMETER gc = 0.1; // coupling conductance (mS/cm^2)
PARAMETER p = 0.1;
PARAMETER Kd = 0.2; // uM
PARAMETER f = 0.01; // percent free to bound Ca
PARAMETER alpha = 0.009; // mol/C/um
PARAMETER kca = 2; // Ca removal rate

// Half Activation voltages in mV, Slopes in MV, Time Constants in milliseconds
PARAMETER Vhm = -35, Sm = -7.8;
PARAMETER Vhh = -55, Sh = 7;
PARAMETER Vhn = -28, Sn = -15;
PARAMETER VhmN = -30, SmN = -5, TaumN = 4;
PARAMETER VhhN = -45, ShN = 5, TauhN = 40;
PARAMETER VhmL = -40, SmL = -7, TaumL = 40;

// Reversal potentials in mV
PARAMETER ENa = 55;
PARAMETER EK = -80;
PARAMETER ECa = 80;
PARAMETER Eleak = -60;


// State Variable Declaration
STATE Vs = 0;
STATE Vd = -60;
STATE h = 1;
STATE n = 0;
STATE mnS = 0;
STATE hnS = 1;
STATE mnD = 0;
STATE hnD = 1;
STATE ml = 0;
STATE CaS = 0;
STATE CaD = 0;
/*
STATE oINaS = 0;
STATE oIKS = 0;
STATE oICaS = 0;
STATE oIleakS = 0;
STATE oIKD = 0;
STATE oICaD = 0;
STATE oIleakD = 0;
STATE oIcouplingS = 0;
STATE oIcouplingD = 0;
STATE oISomaCurrent = 0;
STATE oIDendriteCurrent = 0;
*/


// Functions to store
STATE FUNCTION Tauh, Taun, hinf, ninf, mnSinf, hnSinf, mnDinf, hnDinf, mlinf;
STATE FUNCTION minf;
STATE FUNCTION INaS;
STATE FUNCTION IKS;
STATE FUNCTION ICaS;
STATE FUNCTION IleakS;
STATE FUNCTION IKD;
STATE FUNCTION ICaD;
STATE FUNCTION IleakD;
STATE FUNCTION IcouplingS;
STATE FUNCTION IcouplingD;

// Stimulus current
STATE FUNCTION Iapp;

AT TIME t:

// I_stim is 1V during the specified time range (t_on -- t_off), 
// 0V otherwise  
Iapp   = (t > t_on) ? (t < t_off) ? 11.0: 0.0: 0.0;

    // Steady state values
    Tauh = 30/(exp((Vs+50)/15)+exp(-(Vs+50)/16));
    Taun = 7/(exp((Vs+40)/40)+exp(-(Vs+40)/50));
    minf = 1/(1+exp((Vs-Vhm)/Sm));
    hinf = 1/(1+exp((Vs-Vhh)/Sh));
    ninf = 1/(1+exp((Vs-Vhn)/Sn));
    mnSinf = 1/(1+exp((Vs-VhmN)/SmN));
    hnSinf = 1/(1+exp((Vs-VhhN)/ShN));
    mnDinf = 1/(1+exp((Vd-VhmN)/SmN));
    hnDinf = 1/(1+exp((Vd-VhhN)/ShN));
    mlinf = 1/(1+exp((Vd-VhmL)/SmL));
    INaS = GNa*minf*sqr(minf)*h*(Vs-ENa);
    IKS = (GK_dr*sqr(sqr(n)) + GK_CaS*CaS/(CaS+Kd))*(Vs-EK);
    ICaS = GCa_NS*mnS*mnS*hnS*(Vs-ECa);
    IleakS = gleak*(Vs-Eleak);
    IcouplingS = gc/p*(Vs-Vd);
    IKD = GK_CaD*CaD/(CaD+Kd)*(Vd-EK);
    ICaD = (GCa_ND*mnD*mnD*hnD+GCa_L*ml)*(Vd-ECa);
    IleakD = gleak*(Vd-Eleak);
    IcouplingD = gc/(1-p)*(Vd-Vs);

    // Differential Equations
    d(h) = (hinf-h)/Tauh;
    d(n) = (ninf-n)/Taun;
    d(mnS) = (mnSinf-mnS)/TaumN;
    d(hnS) = (hnSinf-hnS)/TauhN;
    d(mnD) = (mnDinf-mnD)/TaumN;
    d(hnD) = (hnDinf-hnD)/TauhN;
    d(ml) = (mlinf-ml)/TaumL;
    d(CaS) = f*(-alpha*ICaS-kca*CaS);
    d(CaD) = f*(-alpha*ICaD-kca*CaD);
    d(Vs) = 1/C*(Iapp-INaS-IKS-ICaS-IleakS-IcouplingS);
    d(Vd) = 1/C*(-IKD-ICaD-IleakD-IcouplingD);
//    d(Vs) = 1/C*(Iapp-INaS-IKS-ICaS-IleakS);
//    d(Vd) = 1/C*(0);

/*
    oINaS = INaS;
    oIKS = IKS;
    oICaS = ICaS;
    oIleakS = IleakS;
    oIcouplingS = IcouplingS;
    oIKD = IKD;
    oICaD = ICaD;
    oIleakD = IleakD;
    oIcouplingD = IcouplingD;
    oISomaCurrent = INaS+IKS+ICaS+IleakS+IcouplingS-Iapp;
    oIDendriteCurrent = IKD+ICaD+IleakD+IcouplingD;
*/

